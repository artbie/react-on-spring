plugins {
    id 'java'
    id 'idea'
    id 'org.springframework.boot' version '2.6.1'
    id 'io.spring.dependency-management' version '1.0.11.RELEASE'
    id 'com.github.node-gradle.node' version '2.2.4'
    id 'maven-publish'
    id 'net.researchgate.release' version '2.8.1'
    id 'com.palantir.docker' version '0.26.0'
}

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { buildInfo() }

node {
    download = true
    version = '14.16.0'
}

task buildReactApp(type: NpmTask, dependsOn: 'npmInstall') {
    args = ['run-script', 'build']
}
bootJar.dependsOn 'buildReactApp'

clean.delete << file('node_modules')

test {
    useJUnitPlatform()
}

publishing {
    publications {
        mavenPublication(MavenPublication) {
            artifact bootJar
        }

    }
    repositories {
        maven {
            url = "$buildDir/repo"
        }
    }
}

docker {
    name "$project.name:$project.version"
    copySpec.from(bootJar).rename('(.*?)-\\d.*\\.jar', '$1.jar')
    files bootJar
}
